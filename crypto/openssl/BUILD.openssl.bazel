load("@bazel_skylib//rules:common_settings.bzl", "string_flag")
load("@rules_foreign_cc//foreign_cc:defs.bzl", "configure_make")

filegroup(
    name = "src",
    srcs = glob(
        include = ["**"],
        exclude = ["*.bazel"],
    ),
)

string_flag(
    name = "out_lib_dir",
    build_setting_default = "lib64",
)

config_setting(
    name = "out_lib_dir_lib64",
    flag_values = {
        ":out_lib_dir": "lib64",
    },
)

config_setting(
    name = "out_lib_dir_lib",
    flag_values = {
        ":out_lib_dir": "lib",
    },
)

configure_make(
    name = "openssl",
    configure_command = "config",
    configure_in_place = True,
    configure_options = [
        "no-comp",
        "no-idea",
        "no-shared",
        "no-weak-ssl-ciphers",
    ],
    env = select({
        "@platforms//os:macos": {
            "AR": "",
            "PERL": "$$EXT_BUILD_ROOT$$/$(PERL)",
        },
        "//conditions:default": {
            "PERL": "$$EXT_BUILD_ROOT$$/$(PERL)",
        },
    }),
    lib_name = "openssl",
    lib_source = ":src",
    out_binaries = ["openssl"],
    out_lib_dir = select({
        ":out_lib_dir_lib64": "lib64",
        ":out_lib_dir_lib": "lib",
        "//conditions:default": "lib",
    }),
    out_static_libs = [
        "libssl.a",
        "libcrypto.a",
    ],
    target_compatible_with = select({
        "@platforms//os:linux": [],
        "@platforms//os:macos": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    targets = [
        "build_programs",
        "install_sw",
    ],
    toolchains = ["@rules_perl//:current_toolchain"],
    visibility = ["//visibility:public"],
)

filegroup(
    name = "openssl_dir",
    srcs = [":openssl"],
    output_group = "gen_dir",
    visibility = ["//visibility:public"],
)

# runnable_binary(
#     name = "runnable_openssl",
#     binary = "openssl",
#     foreign_cc_target = "@openssl//:openssl",
# )
